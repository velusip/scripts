#!/bin/sh
#########################################################################
#
#   npcmus - outputs what cmus is currently playing.
#   Copyleft (cl) 2010 velusip, velusip@gmail.com
#
#########################################################################
#
#     npcmus requires awk and cmus
#
#   Examples:
#   * irssi - print to channel with /npc
#     /alias npc exec -nosh -window -out - /path/to/npcmus
#
#   * irssi - print only to you with /np
#     /alias np exec -nosh -window - /path/to/npcmus
#
#   * nicotine - print to channel with /np
#     Alt + e, n --> Other --> enter /path/to/npcmus
#
#   * xmobarrc - commands = []
#     Run Com "sh" ["/path/to/npcmus"] "npcmus" 10
#
#########################################################################

cmus-remote -Q | awk '\
function toEnd(iStr, skip)\
{\
    return substr(iStr, match(iStr, skip) + length(skip) + 1);\
}\
function tagless(iStr)\
{\
    return substr(iStr, match(iStr, "/[^/]*/[^/]*$") );\
}\
BEGIN {\
    shuf = "-";\
    icon = "<fc=#f00>*</fc>";\
}\
NR == 1 {\
    if($0 ~ "running") {\
        offline = 1; exit 1\
    }\
}\
NR == 2 && $1 ~ "file" {\
    file = toEnd($0, "file");\
}\
NR != 2 && $2 ~ "^artist$" {\
    if(artist == "")\
    {\
        artist = toEnd($0, "artist");\
    }\
}\
NR != 2 && $2 ~ "^album$" {\
    if(album == "")\
    {\
        album = toEnd($0, "album");\
    }\
}\
NR != 2 && $2 ~ "title" {\
    title = toEnd($0, "title");\
}\
$2 ~ "shuffle" && $3 ~ "true" {\
    shuf = icon;\
}\
$1 ~ "^stream$" {\
    stream = toEnd($0, "stream");\
    sub(/-/, icon, stream);\
}\
END {\
    if(artist && album && title)\
    {\
        print artist, shuf, album, shuf, title\
    }\
    else if(artist && title)\
    {\
        print artist, shuf, title\
    }\
    else if(stream)\
    {\
        print stream\
    }\
    else if(file)\
    {\
        print tagless(file);\
    }\
}'
